//Use this file to  make or modify an existing template. This is the easiest way to modify drill size to you liking for a standard template, or use to tweak the width of the arms for a tighter fit if needed. 
//Uncomment a specific pattern, and then modify any necessary parameters in the primary or secondary parameters section. 




//FORMAT: [(unused),[[holes],[holes],[holes],[holes]], toe/heel/pin line x, nominal length, nominal width, nominal pattern shift, arrow adjust pos, config, label];
//For script output make sure there is one template uncommented to initiate the variable

//---Tech Bindings---


 
//ATK Combined Toe, Source=mfg paper template
//template=[0,[[-13.5,15,0],[-13.5,-15,0],[13.5,15,0],[13.5,-15,0],[-21.7,22.5,0],[-21.7,-22.5,0],[22.3,22.5,0],[22.3,-22.5,0],],0,60,30,0,0,"pin","ATK  Toes"];

    
//ATK combined Heel, Source=mfg paper template
//template=[0,[[-62.5,12.5,0],[-62.5,-12.5,0],[26,22.5,0],[26,-22.5,0],[23.5,12.5,0],[23.5,-12.5,0],[-36.5,12.5,0],[-36.5,-12.5,0],[-2.5,12.5,0],[-2.5,-12.5,0],[-48.9,12.5,0],[-48.9,-12.5,0],[10.5,22.5,0],[10.5,-22.5,0],[-49.5,22.5,0],[-49.5,-22.5,0],],0,115,60,15,0,"tech_heel","ATK Heels"];


//Dynafit most common toe [30 x 39], Source=various
//template=[0,[[0,15,0],[0,-15,0],[39,15,0],[39,-15,0]],13.25,75,50,-5,0,"pin","Dynafit  most common toe [30 x 39]"];

//Dynafit vertical & turn toe [30 x 26.5 (+18)], Source=Knut Pohl
//template=[0,[[0,15,0],[0,-15,0],[26.5,15,0],[26.5,-15,0],[44.5,0,0]],13.25,75,50,-10,0,"pin","Dynafit  vertical & turn toe [30 x 26.5 (+18)]"];

//Dynafit ST&TLT heel [32&36 x 52.5], Source=Knut Pohl
//template=[0,[[0,18,0],[0,-18,0],[52.5,16,0],[52.5,-16,0]],42.25,80,50,12,0,"tech_heel","Dynafit  ST&TLT heel [32&36 x 52.5]"];

//Dynafit Superlite Heel [34&28 x 28], Source=thom
//template=[0,[[0,14,0],[0,-14,0],[28,17,0],[28,-17,0]],36.2,70,50,20,0,"tech_heel","Dynafit Superlite heel [32_36 x 52.5]"];

//Dynafit radical 2.0&rotation, Source=slidewright/alpinord
//template=[0,[[0,21,0],[0,-21,0],[57.5,21,0],[57.5,-21,0]],27,90,55,-5,0,"pin","Dynafit  Radical 2.0&Rotation Toe"];
//template=[0,[[0,17.75,0],[0,-17.75,0],[88,17.75,0],[88,-17.75,0]],43,115,50,-5,0,"tech_heel","Dynafit  radical 2.0&rotation heel"];

//G3 Ion & Zed, Source=various, split dims between different conflicting layouts
//template=[0,[[0,20,0],[0,-20,0],[45,20,0],[45,-20,0]],20.5,80,55,-5,0,"pin","G3 Ion & Zed Toe"];
//template=[0,[[0,18.5,0],[0,-18.5,0],[52.5,18.5,0],[52.5,-18.5,0]],37.5,80,50,8,0,"tech_heel","G3 Ion & Zed Heel"];

//Marker Kingpin, Source=Knut Pohl, pin line not verified & middle hole ommited
//template=[0,[[0,19,0],[0,-19,0],[46,19,0],[46,-19,0]],23.5,75,55,-3,0,"pin","Marker  Kingpin Toe"];
//template=[0,[[0,18,0],[0,-18,0],[77,18,0],[77,-18,0]],42,105,55,0,0,"tech_heel","Marker  Kingpin heel"];
    
//Marker alpinist, Source=Binding Freedom Layout, heel stop position not verified
//template=[0,[[0,19,0],[0,-19,0],[46,19,0],[46,-19,0]],26,75,55,0,0,"pin","Marker  Alpinist Toe"];
//template=[0,[[0,18,0],[0,-18,0],[40,18,0],[40,-18,0]],40,70,45,20,0,"tech_heel","Marker  Alpinist Heel"];

//PLUM Toe [30 x 26.5], Source=mfg paper template
//template=[0,[[0,15,0],[0,-15,0],[26.5,15,0],[26.5,-15,0]],13.25,70,50,0,0,"pin","PLUM Toe [30 x 26.5]"];

//PLUM Toe [36 x 69], Source=mfg paper template
//template=[0,[[0,18,0],[0,-18,0],[69,18,0],[69,-18,0]],34.5,100,50,-4,0,"pin","PLUM Toe [36 x 69]"];

//PLUM R99 / R120 / R150, Source=mfg paper template
//template=[0,[[0,10.25,0],[0,-10.25,0],[26,10.25,0],[26,-10.25,0]],30.5,60,50,15,0,"tech_heel","PLUM R99 & R120 & R150 Heel"];

//PLUM R170 / OAZO, Source=mfg paper template
//template=[0,[[0,12.5,0],[0,-12.5,0],[55,12.5,0],[55,-12.5,0]],45,90,40,15,0,"tech_heel","PLUM R170 & OAZO Heel"];

//PLUM 40mm plate, Source=mfg paper template
//template=[0,[[0,12.5,0],[0,-12.5,0],[86,12.5,0],[86,-12.5,0]],60.5,120,40,15,0,"tech_heel","PLUM 40mm race plate"];

//PLUM Guide/WEPA/PIKA heel, Source=mfg paper template
//template=[0,[[0,18,0],[0,-18,0],[53,16,0],[53,-16,0]],46.65,80,50,16,0,"tech_heel","PLUM Guide WEPA PIKA Heel [32 x 53]"];

//PLUM Summit Heel [36 x 72], Source=mfg paper template
//template=[0,[[0,18,0],[0,-18,0],[72,18,0],[72,-18,0]],55.65,100,50,16,0,"tech_heel","PLUM Summit Heel [36 x 72]"];

//PLUM Yak, Source=mfg template
//template=[0,[[0,25,0],[0,-25,0],[50,25,0],[50,-25,0]],25,72,45,-4,0,"pin","PLUM Yak Toe"];
//template=[0,[[0,27.5,0],[0,-27.5,0],[58,27.5,0],[58,-27.5,0]],48.6,80,50,16,0,"tech_heel","PLUM Yak Heel"];

//Salomon MTN/ atomic Backland, Source=Knut Pohl
//template=[0,[[0,20,0],[0,-20,0],[38,20,0],[38,-20,0]],19.5,75,55,0,0,"pin","Salomon  MTN toe"];
//template=[0,[[0,13.5,0],[0,-13.5,0],[58,13.5,0],[58,-13.5,0]],45.5,90,55,13,0,"tech_heel","Salomon  MTN heel"];
    
//Salomon shift, Source=Knut Pohl
//template=[0,[[0,15,0],[0,-15,0],[70,20,0],[70,-20,0],[135,0,0]],90,165,60,20,30,"toe","Salomon Shift Toe"];
//template=[0,[[0,18,0],[0,-18,0],[68,18,0],[68,-18,0]],53,100,55,18,0,"tech_heel","Salomon Shift Heel"];

////Ski Trab Heel (toes match ATK narrow toe), Source=Gavin Hess (The High Route) patterns in order:vario.2/race plate/gara
//template=[0,[[0,22.75,0],[0,-22.75,0],[37.5,22.75,0],[37.5,-22.75,0],[-20,18,0],[-20,-18,0],[55,18,0],[55,-18,0],[1,12.5,0],[1,-12.5,0],[32,12.5,0],[32,-12.5,0]],33,100,70,11,0,"tech_heel","Ski Trab Heel - Vario.2, Gara, Plate"];

//22 Designs Telemark Bindings
//template=[0,[[0,19.05,0],[0,-19.05,0],[38.1,19.05,0],[38.1,-19.05,0],[76.2,19.05,0],[76.2,-19.05,0]],71.628,110,58,25,20,"toe","22 Designs Toe - Vice,Outlaw,Lynx"];
//template=[0,[[0,19.05,0],[0,-19.05,0],[38.1,19.05,0],[38.1,-19.05,0],[76.2,19.05,0],[76.2,-19.05,0]],63.628,102,58,20,20,"toe","22 Designs Toe - AXL"];
//template=[0,[[25.4,0,0],[63.5,0,0]],0,110,30,-30,52,"tech_heel","22 Designs Heel"];


//Fritschi Vipec, Source=Knut Pohl
//template=[0,[[0,20,0],[0,-20,0],[66,22,0],[66,-22,0]],34,90,45,-3,0,"pin","Fritschi Vipec Toe"];
//template=[0,[[0,14,0],[0,-14,0],[60,14,0],[60,-14,0]],43.5,83,35,10,0,"tech_heel","Fritschi Vipec Heel"];
    
//---Alpine Bindings---  

//Look Pivot & SPX Toe, Source=Knut Pohl
//template=[1,[[0,17.5,0],[0,-17.5,0],[41.5,21,0],[41.5,-21,0]],10,68,42,-15,32,"toe","Look Pivot & SPX Toe"];
    
//Look Pivot Heel, Source=Knut Pohl (heel differs by ~5mm from BF template?
template=[0,[[0,15,0],[0,-15,0],[32,10.5,0],[32,-10.5,0]],-55,125,45,-40,30,"heel","Look  Pivot Heel"];

//Look PX & SPX Heel, Source=Knut Pohl
//template=[0,[[0,21,0],[0,-21,0],[105,21,0],[105,-21,0]],79,130,45,23,30,"heel","Look  SPX Heel"];
   
//Marker Squire/Griffon/Duke PT, Source=Knut Pohl, DUKE PT TGR THREAD conflicting toe line  (-6 vs -7.5 vs -9.6) meaning either kingpin or J/G/S toe line is off by a mm or two.  https://www.tetongravity.com/forums/showthread.php/331488-Duke-PT-hole-pattern/page2?highlight=duke+template
//template=[0,[[0,18,0],[0,-18,0],[31,18,0],[31,-18,0]],10,65,45,-8,-5,"toe","Marker Squire Griffon Toe"];
//template=[0,[[0,19,0],[0,-19,0],[46,19,0],[46,-19,0]],-7.5,75,45,-32,37,"toe","Marker Duke PT Toe"];
//template=[0,[[0,16,0],[0,-16,0],[80,16,0],[80,-16,0]],55,110,45,12,0,"heel","Marker Squire Griffon DukePT Heel"];

//Marker M-series, Source=Knut Pohl
//template=[0,[[0,0,0],[56,18,0],[56,-18,0]],67,110,60,25,15,"toe","Marker M-Series Toe"];
//template=[0,[[0,21,0],[0,-21,0],[80,10.5,0],[80,-10.5,0]],65,105,55,22,0,"heel","Marker M-Series Heel)"];

//Salomon warden 11 heel, Source=Knut Pohl, but corrected heel location
//template=[0,[[0,15,0],[0,-15,0],[80,15,0],[80,-15,0]],56,110,40,13,0,"heel","Salomon  Warden 11 Heel"];

//Salomon warden 13 & 11 toes, Source=Knut Pohl
//template=[0,[[0,20,0],[0,-20,0],[65,20,0],[65,-20,0]],15,100,60,-20,0,"toe","Salomon  Warden 13 & 11 Toe"];


//Salomon STH2 toes, Source=Knut Pohl, updated 12/27/22 TGR/Westcoaster, deleted old sth z-type hole
//template=[0,[[0,20,0],[0,-20,0],[31,21.25,0],[31,-21.25,0]],15,70,60,0,0,"toe","Salomon  STH2 Toes"];

//Salomon warden 13/STH2 heels, Source=Knut Pohl
//template=[0,[[0,16,0],[0,-16,0],[75,16,0],[75,-16,0]],47,110,45,10,0,"heel","Salomon  Warden 13 & STH2 Heel"];

//Salomon Alpine (916,920, etc), Source=Knut Pohl
//template=[0,[[0,20,0],[0,-20,0],[30,22,0],[30,-22,0],[75,0,0]],15,105,60,-25,-5,"toe","Salomon Alpine Toe (916,920, etc)"];
//template=[0,[[0,21.,0],[0,-21,0],[92,14,0],[92,-14,0]],51,125,55,0,0,"heel","Salomon Alpine Heel (916,920, etc)"];

//Tyrolia attack, Source=Knut Pohl (Note 5mm diff toe from BF, I think this is the correct one though)
//template=[0,[[0,20,0],[0,-20,0],[55,20,0],[55,-20,0]],15,85,55,-20,40,"toe","Tyrolia Attack Toe"];
//template=[0,[[0,21.25,0],[0,-21.25,0],[95,10,0],[95,-10,0]],75,118,55,25,0,"heel","Tyrolia Heel"];



//---Demo Bindings (each requires specific length setting spacer)

//warden 11 & 13 DEMO toe, source=gregorys
//template=[0,[[0,15,0],[0,-15,0],[82,15,0],[82,-15,0]],0,110,45,-42,70,"demo_toe","Salomon  Warden 11&13 Demo Toe"];

//warden 13 DEMO heel, source=gregorys
//template=[0,[[0,16.5,0],[0,-16.5,0],[105,16,0],[105,-16,0]],105,135,50,50,0,"demo_heel","Salomon  Warden 13 Demo Heel"];


//warden 11 DEMO heel, source=gregorys
//template=[0,[[0,15,0],[0,-15,0],[80,15,0],[80,-15,0]],80,110,45,40,0,"demo_heel","Salomon  Warden 11 Demo Heel"];




//Tyrolia Attack DEMO, source=gregorys
//template=[0,[[0,17,0],[0,-17,0],[100,17,0],[100,-17,0]],0,130,45,-55,40,"demo_toe","Tyrolia Attack Demo Toe"];
//template=[0,[[0,21.5,0],[0,-21.5,0],[95,10,0],[95,-10,0]],95,125,45,45,0,"demo_heel","Tyrolia Attack Demo Heel"];



binding_plate(template);



// ---------PRIMARY PARAMTERS---------
//HOLE SIZE
hole_diam=9.0; 
hole_chamfer=0.5;
hole_shoulder=1;//change this to zero if not using a sleeve/bushing

//template arms
arm_width=146.7; //adjust for small width difference in jig to make template a tight fit




//The following primary parameters are controlled by whichever template is uncommented above. If multiple it will be the last one. 
//CONFIGURATION
config=template[7]; //turns on or off features for toe stop, heel stop, or pins | options: "toe", "heel", "pin", "pin_toe" (pin and toe stop features), or "none". Something other than the listed option will result in no features (same as "none") 

//TEXT LABEL
label_text= template[8]; //MFG, model, heel/toe, etc

//BASE PLATFORM DIMENSIONS
length=template[3]; 
width=template[4];
shift_pattern=template[5]; //shift the entire pattern forwards or back to keep template small
arrow_x_adjust=template[6]; //move arrow forward or back as needed to avoid features


// ---------END PRIMARY PARAMTERS---------

//Secondary Paramters - Normally shouldn't need adjustment
$fn=30;


//body
ext_chamfer_size=-.75; //corner
lead_in_chamfer_size=1; //bottom
overlap=1;
height=20.3;
arm_length=12.7;
arm_radius=1;
center_mark_size=3;
int_chamfer_size=15; //internal corners between body and arms

//ref lines
ref_line_width=width-10;
ref_line_size=1;
ref_line_depth=.5;

//holes and z-seam clearance, shoulder at bottom of hole
clearance=.5;
clearance_angle=180;
shoulder_height=1*hole_shoulder; 
shoulder_rad=.3; //diference from larger hole size, not actual radius. 


//Toe
screw_size=4.5; //heel and toe
hex_size=7.8; //heel and toe
hex_depth=13; //heel and toe

stop_offset=.125*25.4; 
stop_width=9.91;
stop_length=16.13;
stop_depth=2.29; //heel and toe
stop_hole_shift=0; //hole to rect cut centered or not?

//Heel(most are similar to toe features)
h_stop_offset=.125*25.4;
h_stop_width=9.91;
h_stop_length=16.13;
h_stop_hole_shift=0; //hole to rect cut centered or not?

//pins
pin_x=shift_pattern;

bar_offset=38;
bar_width=4.78;
bar_hole_size=4.5;
hole_y=0;
hole_height=height/2;

notch_width=10.16;
notch_depth=3.18;
notch_boss=3;
bar_height=height;


//text
margin=2;
text_loc=[length/2-arm_length/2,0,height];

text_depth=.5;

L_loc=[-length/2+arm_length/2,arm_width/2-margin,height];
R_loc=[-length/2+arm_length/2,-(arm_width/2-margin),height];
arrow_symbol="â†‘";
arrow_size=25;
arrow_loc=[-length/2+arm_length/2+arrow_x_adjust,0,height];
text_size=5;

demo_loc=[0,+10,height];
demo_text="Demo Template";
demo_loc_l2=[0,-10,height];
demo_text_l2="Use Spacer";

module binding_plate (template){

// ---------SHAPES AND LOCATIONS---------
bar_hole=[pin_x,bar_offset,height];

toe=[1,0,0,1,0];
heel=[0,1,0,1,0];
pin=[0,0,1,0,0];
pin_toe=[1,0,1,1,0];
none=[0,0,0,0,0];
demo_toe=[1,0,0,1,1];
demo_heel=[0,1,0,1,1];

config_list=[toe, heel, pin, pin_toe, none, demo_toe,demo_heel];
config_set= (config=="toe") ? config_list[0]:(config=="heel") ? config_list[1]:(config=="tech_heel") ? config_list[1]: (config=="pin") ? config_list[2]:(config=="pin_toe") ? config_list[3]: (config=="demo_toe") ? config_list[5] : (config=="demo_heel") ? config_list[6]: config_list[4];


td=config_set[0]; //toe features on/off
hd=config_set[1]; //heel features on/off
pd=config_set[2]; //pin features on/off
rd=config_set[3]; //ref line on/off, used for heel and toe
dd=config_set[4]; //demo text on/off

shape=[
[length/2,arm_width/2,ext_chamfer_size],
[length/2,-arm_width/2,ext_chamfer_size],
[length/2-arm_length,-arm_width/2,ext_chamfer_size],
[length/2-arm_length,-width/2,int_chamfer_size],
[-(length/2-arm_length),-width/2,int_chamfer_size],
[-(length/2-arm_length),-arm_width/2,ext_chamfer_size],
[-length/2,-arm_width/2,ext_chamfer_size],
[-length/2,arm_width/2,ext_chamfer_size],
[-(length/2-arm_length),arm_width/2,ext_chamfer_size],
[-(length/2-arm_length),width/2,int_chamfer_size],
[(length/2-arm_length),width/2,int_chamfer_size],
[(length/2-arm_length),arm_width/2,ext_chamfer_size],
];

//heel and toe stop actuall positions
hole_forward=stop_length/2+stop_offset;
h_hole_forward=-h_stop_length/2-h_stop_offset;

// ---------MODELING---------
union(){
union(){
difference(){
    //base shape
    difference(){
        //center mark and base shape
        linear_extrude (height = height) chamfer_corners(shape);
        union(){
        translate([length/2,0,0])   
                rotate([0,0,45])
                cube([center_mark_size,center_mark_size,height*2.1],center=true);
        translate([-length/2,0,0])  
                rotate([0,0,-45])
                cube([center_mark_size,center_mark_size,height*2.1],center=true);
        translate([0,arm_width/2,0])  
                rotate([45,0,])
                cube([length,lead_in_chamfer_size*2,lead_in_chamfer_size*2],center=true);
        translate([0,-arm_width/2,0])  
                rotate([45,0,])
                cube([length,lead_in_chamfer_size*2,lead_in_chamfer_size*2],center=true);
            
    }}
    //holes and reference features
    translate([shift_pattern,0,0]){
        union (){
            translate([-template[2],0,0]) //negative to shift pattern backwards
                union(){
                    for (h=[0:len(template[1])-1])
                        echo(template[1][h])
                        hole_w_clearance (template[1][h],hole_diam/2,height,clearance,clearance_angle,hole_chamfer,overlap,shoulder_height, shoulder_rad);
                           }
//            //reference line
//            hull(){
//                translate([0,ref_line_width/2,height-ref_line_depth])cylinder(h=(ref_line_depth+overlap)*rd,r=ref_line_size/2);
//                translate([0,-ref_line_width/2,height-ref_line_depth])cylinder(h=(ref_line_depth+overlap)*rd,r=ref_line_size/2);
//            }
            //toe features
            ref_hole_center=[hole_forward,0,0]; 
            hex_hole (ref_hole_center,hex_size,screw_size,hex_depth*td,height*td,1,overlap*td);
            translate([ref_hole_center.x+stop_hole_shift,0,height-stop_depth/2]){
                cube([stop_length,stop_width,(stop_depth+overlap)*td],center=true);
            }
            //heel features
            h_ref_hole_center=[h_hole_forward,0,0]; 
            hex_hole (h_ref_hole_center,hex_size,screw_size,hex_depth*hd,height*hd,1,overlap*hd);
            translate([h_ref_hole_center.x+h_stop_hole_shift,0,height-stop_depth/2]){
            cube([h_stop_length,h_stop_width,(stop_depth+overlap)*hd],center=true);
            }}
        }}

 

 
 
//toe pin features
copy_mirror(vec=[0,1,0]){;
    translate([0,bar_offset,bar_height/2]){
        union(){
        difference(){
            cube([length,bar_width*pd, bar_height], center=true);
            translate([pin_x,0,hole_y/2]){    //center hole in main part of bar
                rotate([90,0,0])
                    cylinder(bar_width+2*overlap*pd,d=bar_hole_size,center=true);
                    translate([0,0,(height/2-notch_depth/2)])
                        cube([notch_width,bar_width+overlap,notch_depth+overlap],center=true);
        }}
            translate([pin_x,0,-height/2+bar_width/2])
                rotate([0,0,0])
                    for(e=[1,-1]){ 
                        bar_notch=[[e*(notch_width/2),0],[e*(notch_width/2),-(notch_depth+bar_width/2)],[e*(notch_width/2+notch_boss),-(notch_depth+bar_width/2)],[e*(notch_width/2+notch_boss+notch_depth),0]];    
                    linear_extrude(height=bar_width*pd,center=true) polygon(bar_notch);
}}}}    

//Text cuts
difference(){
union(){
translate(text_loc)
rotate([0,0,-90])
linear_extrude(text_depth*2,center=true) text(label_text,text_size,font="Arial:style=Bold",halign="center", valign="center");

translate(arrow_loc)
rotate([0,0,-90])
linear_extrude(text_depth*2,center=true) text(arrow_symbol,arrow_size,halign="center", valign="bottom");

translate(L_loc)
rotate([0,0,-90])
linear_extrude(text_depth*2,center=true) text("L",text_size,halign="left", valign="center");

translate(R_loc)
rotate([0,0,-90])
linear_extrude(text_depth*2,center=true) text("R",text_size,halign="right", valign="center");
    
translate(demo_loc)
rotate([0,0,0])
linear_extrude(text_depth*2*dd,center=true) text(demo_text,text_size,halign="center", valign="center");
    
translate(demo_loc_l2)
rotate([0,0,0])
linear_extrude(text_depth*2*dd,center=true) text(demo_text_l2,text_size,halign="center", valign="center"); 

 //reference line
  translate([shift_pattern,0,0]){
    hull(){
        translate([0,ref_line_width/2,height])cylinder(h=(ref_line_depth)*rd,r=ref_line_size/2);
        translate([0,-ref_line_width/2,height])cylinder(h=(ref_line_depth)*rd,r=ref_line_size/2);
            }}
            
//pin ref line
translate([pin_x,0,height]){
    hull(){
        translate([0,ref_line_width/2,0])cylinder(h=(ref_line_depth+overlap)*pd,r=ref_line_size/2,center=true);
        translate([0,-ref_line_width/2,0])cylinder(h=(ref_line_depth+overlap)*pd,r=ref_line_size/2,center=true);
 }}}
//clearnance around holes
translate([shift_pattern,0,0]){
        translate([-template[2],0,height-ref_line_depth]) //negative to shift pattern backwards, height to get on top
            union(){
                for (h=[0:len(template[1])-1])
                    translate(template[1][h]){
                        cylinder(h=ref_line_depth*3, d=hole_diam+2);}
                           }} 
}  
     
}}}

 




// ---------MODULES---------

//camfers 90 deg corners, with square rotated to 45deg. Use on 2d shapes only
// 3rd data point for size, +/- for internal vs external
module chamfer_corners(points){
    l=len(points);
    difference() {  
        union() {
            for (i=[0:l-1])
                if (points[i].z>0){
                    lastx= points[i-1].x==undef ? points[l-1].x:points[i-1].x;            
                    lasty= points[i-1].y==undef ? points[l-1].y:points[i-1].y;
                    nextx= points[i+1].x==undef ? points[0].x:points[i+1].x;
                    nexty= points[i+1].y==undef ? points[0].y:points[i+1].y;
                    
                    segment_length_last=((lastx-points[i].x)^2+(lasty-points[i].y)^2)^0.5;
                    segment_length_next=((nextx-points[i].x)^2+(nexty-points[i].y)^2)^0.5;

                    polygon([[points[i].x, points[i].y],
                    [points[i].x-(points[i].x-lastx)*abs(points[i].z)/segment_length_last,points[i].y-(points[i].y-lasty)*abs(points[i].z)/segment_length_last], //chamfer point on last segment
                    [points[i].x+(nextx-points[i].x)*abs(points[i].z)/segment_length_next,points[i].y+(nexty-points[i].y)*abs(points[i].z)/segment_length_next] //chamfer point on next segment
                    ]);
                    }   
               polygon([for(i=[0:l-1])[points[i].x,points[i].y]]);
                }
        union() {
            for (i=[0:l-1])            
                if (points[i].z<0){
                    lastx= points[i-1].x==undef ? points[l-1].x:points[i-1].x;            
                    lasty= points[i-1].y==undef ? points[l-1].y:points[i-1].y;
                    nextx= points[i+1].x==undef ? points[0].x:points[i+1].x;
                    nexty= points[i+1].y==undef ? points[0].y:points[i+1].y;
                    
                    segment_length_last=((lastx-points[i].x)^2+(lasty-points[i].y)^2)^0.5;
                    segment_length_next=((nextx-points[i].x)^2+(nexty-points[i].y)^2)^0.5;

                    polygon([[points[i].x, points[i].y],
                    [points[i].x-(points[i].x-lastx)*abs(points[i].z)/segment_length_last,points[i].y-(points[i].y-lasty)*abs(points[i].z)/segment_length_last], //chamfer point on last segment
                    [points[i].x+(nextx-points[i].x)*abs(points[i].z)/segment_length_next,points[i].y+(nexty-points[i].y)*abs(points[i].z)/segment_length_next] //chamfer point on next segment
                    ]);   
                    }
                }
            }      
}
    


//Hole module for Z-seam clearance | args: center loc, radius, height, radial z-seam clearance, angle of z-seam clearance, hole chamfer size, vertical overlap for modeling, shoulder height, and shoulder radial diff
module hole_w_clearance (c,r,h,cl,angle,hc,vc,sh,sr){
    theta=acos(r/(r+cl));
    translate([0,0,-vc]){
    translate(c){
        rotate(a=angle) {
            difference(){
                union(){
                linear_extrude(height=h+2*vc){
                    polygon(points=[[0,0],[r*cos(theta),r*sin(theta)],[r+cl,0],[r*cos(theta),-r*sin(theta)]]);}
                cylinder(h+2*vc,r,r);
                translate([0,0,h+vc-hc])    
                    cylinder(hc+vc,r,(r+hc+vc));
                }
                //small shoulder for insert
                translate ([0,0,-cl]) {
                    difference(){
                        cylinder(sh+2*cl,r+hc*4,r+hc*4);
                        cylinder(sh+cl*2,r-sr,r-sr);
}}}}}}}

//module for hole with hex for press in nut
module hex_hole (c,s,d,cb,h,dir,vc){ //through hole with hex for nut |args: center, size of hex, depth of hex (counterbore),hole dia direction of hole (1 or -1), height of through hole, vetrical clearance
    flip= dir==-1 ? 180:0;
    translate([c.x,c.y,(c.z-vc)*dir]){
        rotate([0,flip,0])
        union(){
        hull(){
        for(i=[0:60:360])
            rotate(i){
            translate([s/2,0,0])
                cylinder(cb+vc,r=0.25);
            }}   
        cylinder((h+2*vc),r=d/2);
 }}}
 
 
 module copy_mirror(vec=[0,1,0]){
    children();
    mirror(vec) children();
} 